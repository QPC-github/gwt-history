plugins {
  id "java-library"
  id "maven"
  id "net.ltgt.errorprone" version "0.0.13"
  id "com.github.sherter.google-java-format" version "0.6"
}

group = "org.gwtproject.user.history"
version = "HEAD-SNAPSHOT"

repositories {
  mavenCentral()
}

dependencies {
  errorprone "com.google.errorprone:error_prone_core:2.1.2"

  api "org.gwtproject.event:gwt-logical-event:HEAD-SNAPSHOT"
  implementation "org.gwtproject.user.window:gwt-window:HEAD-SNAPSHOT"
  implementation "com.google.elemental2:elemental2-dom:1.0.0-beta-1"
  implementation "com.google.elemental2:elemental2-core:1.0.0-beta-1"

  testImplementation "junit:junit:4.12"
  testImplementation "com.google.gwt:gwt-user:2.8.2"
  testImplementation "com.google.gwt:gwt-dev:2.8.2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs << "-Werror" << "-Xlint:all"
}

jar {
  from sourceSets.main.allJava
}

test {
  def warDir = file("$buildDir/gwt/www-test")
  def workDir = file("$buildDir/gwt/work")
  def cacheDir = file("$buildDir/gwt/cache")
  doFirst {
    mkdir(warDir)
    mkdir(workDir)
    mkdir(cacheDir)
  }

  classpath += sourceSets.main.allJava.sourceDirectories + sourceSets.test.allJava.sourceDirectories
  include "**/*Suite.class"
  systemProperty("gwt.args", "-ea -draftCompile -batch module -war \"$warDir\" -workDir \"$workDir\" " +
          "-runStyle ${project.findProperty('test.gwt.runStyle') ?: 'HtmlUnit:Chrome'}")
  systemProperty("gwt.persistentunitcachedir", cacheDir)
  testLogging {
    events('STANDARD_OUT')
  }
}

task validateGwtModule(type: JavaExec) {
  def workDir = file("$buildDir/gwt/work")
  def cacheDir = file("$buildDir/gwt/cache")
  def outFile = file("$buildDir/gwt/validateGwtModule")
  doFirst {
    mkdir(workDir)
    mkdir(cacheDir)

    standardOutput = errorOutput = outFile.newOutputStream()
  }
  doLast {
    standardOutput.close()
  }

  inputs.files sourceSets.main.allJava
  outputs.file outFile

  main = 'com.google.gwt.dev.Compiler'
  classpath = sourceSets.test.runtimeClasspath + sourceSets.main.allJava.sourceDirectories
  args '-strict', '-validateOnly', '-workDir', workDir, 'org.gwtproject.user.history.History'
  systemProperty("gwt.persistentunitcachedir", cacheDir)
}
check.dependsOn validateGwtModule

javadoc {
  options.addBooleanOption("Xdoclint:all,-missing", true)
}

googleJavaFormat {
  toolVersion = "1.5"
}

// Pending deployment on Sonatype OSS or other "real" Maven repository, use Jitpack for snapshots
repositories {
  maven { url 'https://jitpack.io/' }
}
configurations.all*.resolutionStrategy*.dependencySubstitution {
  substitute module('org.gwtproject.event:gwt-logical-event') with module('com.github.tbroyer.gwt-events:gwt-logical-event:-SNAPSHOT')
  substitute module('org.gwtproject.user.window:gwt-window') with module('com.github.tbroyer:gwt-window:-SNAPSHOT')
  // transitive dependency of gwt-window
  substitute module('org.gwtproject.http:gwt-http') with module('com.github.tbroyer:gwt-http:-SNAPSHOT')
}
